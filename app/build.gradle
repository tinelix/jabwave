buildscript {
    dependencies {
        classpath project(':app')
    }
}

plugins {
    id 'com.android.application'
}

def getGitHubCommit = {
    return "UNKNOWN";
}

def getTDAppToken = {
    /*
         HOW TO STORE TELEGRAM API KEY SAFELY?

         1.  Create file named 'tdlib.prop' in '.signing' directory.
         2.  Generate Base58 hash according to the format of the original string:
             '<api_id>.<api_hash>'
             (without quotation marks)
         3.  Store 'tdlib.prop' in a place inaccessible to third parties.
    */
    Properties tdlib_props = new Properties();
    if (file('.signing/tdlib.prop').exists()) {
        tdlib_props.load(new FileInputStream(file(".signing/tdlib.prop")))
        return "${tdlib_props['APP_TOKEN']}"
    } else {
        return "UNKNOWN.UNKNOWN"
    }
}

android {
    namespace 'dev.tinelix.jabwave'
    compileSdk = 36

    defaultConfig {
        applicationId "dev.tinelix.jabwave"
        minSdk 19
        targetSdk 36
        versionCode 1
        versionName "0.0.1-prealpha"
        multiDexEnabled true
        buildConfigField "String", "GIT_COMMIT", "\"${getGitHubCommit()}\""
        configurations.configureEach {
            resolutionStrategy {
                force 'androidx.core:core:1.12.0'
            }
        }
        buildConfigField "String", "SOURCE_CODE", "\"https://github.com/tinelix/jabwave\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_16
        targetCompatibility JavaVersion.VERSION_16
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions 'version'
    productFlavors {
        smack {
            dimension 'version'
            versionNameSuffix '.smack'
            applicationId 'dev.tinelix.jabwave.sm'
            buildConfigField "String", "BUILD_VARIANT", "\"smack\""
        }
        combo {
            dimension 'version'
            buildConfigField "String", "TDLIB_APP_TOKEN", "\"${getTDAppToken()}\""
            buildConfigField "String", "BUILD_VARIANT", "\"combo\""
        }
    }

    sourceSets {
        smack {
            java.srcDirs = ['src/smack/java']
            res.srcDirs = ['src/smack/res']
            manifest.srcFile 'src/smack/AndroidManifest.xml'
        }
        combo {
            java.srcDirs = ['src/combo/java']
            res.srcDirs = ['src/combo/res']
            manifest.srcFile 'src/combo/AndroidManifest.xml'
        }
    }
}

dependencies {
    comboImplementation 'org.jxmpp:jxmpp-jid:1.0.3'
    comboImplementation "org.igniterealtime.smack:smack-tcp:4.4.8"
    comboImplementation "org.igniterealtime.smack:smack-im:4.4.8"

    // Smack build-specified dependencies
    smackImplementation 'org.jxmpp:jxmpp-jid:1.0.3'
    smackImplementation "org.igniterealtime.smack:smack-tcp:4.4.8"
    smackImplementation "org.igniterealtime.smack:smack-im:4.4.8"

    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.6.1'
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.11.0'
    //noinspection GradleDependency
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-fragment:2.7.7'
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-ui:2.7.7'
    implementation "androidx.preference:preference-ktx:1.2.1"

    implementation ("org.igniterealtime.smack:smack-android-extensions:4.4.8") {
        exclude module: 'xpp3'
    }
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation project(path: ':api')

    annotationProcessor ('com.github.bumptech.glide:compiler:5.0.5')
    implementation 'com.github.luizgrp:SectionedRecyclerViewAdapter:v3.2.0'
    //noinspection GradleDependency
    implementation "androidx.emoji2:emoji2:1.4.0"
    implementation 'com.github.mediapark-pk:Base58-android:0.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
}